apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

def getVersionCode() { // 获取版本号
    def versionFile = file('version.properties')// 读取第一步新建的文件
    if (versionFile.canRead()) {// 判断文件读取异常
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps['VERSION_CODE'].toInteger()// 读取文件里面的版本号
        def runTasks = gradle.startParameter.taskNames
        if (':app:assembleRelease' in runTasks) {//仅在assembleRelease任务是增加版本号,其他渠道包在此分别配置
            // 版本号自增之后再写入文件（此处是关键，版本号自增+1）
            versionProps['VERSION_CODE'] = (++versionCode).toString()
            versionProps.store(versionFile.newWriter(), null)
        }
        return versionCode // 返回自增之后的版本号
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

def getVersionName() {
    def date = new Date()
    def versionName = date.format('yyyyMMddHHmmss')
    return versionName
}

def currentVersionCode = getVersionCode()
def currentVersionName = getVersionName()

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sgeye.eyefile.pad"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode currentVersionCode
        versionName currentVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    buildToolsVersion '27.0.3'

    signingConfigs {
        release {
            storeFile file(MYAPP_RELEASE_STORE_FILE)
            storePassword MYAPP_RELEASE_STORE_PASSWORD
            keyAlias MYAPP_RELEASE_KEY_ALIAS
            keyPassword MYAPP_RELEASE_KEY_PASSWORD
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':margaret-core')
    // ButterKnife
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    // gif
    api 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
    // 动态权限
    api 'com.karumi:dexter:5.0.0'
    // QR Code
    api 'com.google.zxing:core:3.3.3'
    api 'cn.bingoogolapple:bga-qrcodecore:1.1.7@aar'
    api 'cn.bingoogolapple:bga-zxing:1.1.7@aar'
    implementation "org.java-websocket:Java-WebSocket:1.3.6"


    implementation 'com.android.support:cardview-v7:27.1.1'
}
